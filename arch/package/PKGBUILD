# Maintainer: stazed<stazed@mapson.com>
# To use:makepkg -si
_pkgbasename=seq42
pkgname=${_pkgbasename}-git
pkgver=2.0.0
pkgrel=1
pkgdesc="A MIDI sequencer"
arch=('i686' 'x86_64')
url="http//github.com/Stazed/seq42.git"
license=('GPL3')
depends=('gtkmm3' 'jack' 'liblo')
makedepends=('git')
provides=("${_pkgbasename}")
conflicts=("${_pkgbasename}")
source=("${_pkgbasename}::git://github.com/Stazed/seq42.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgbasename}"

    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    mkdir -p "${srcdir}/build"
    cd "${srcdir}/build"
    cmake "${srcdir}/${_pkgbasename}" \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX="/usr"
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
}
